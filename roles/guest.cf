body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/common/mounts.cf",
              "cryptoo/common/systemd.cf",
              "cryptoo/config/directory_groups.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/config/templates.cf",
              "cryptoo/util/contain.cf",
              "cryptoo/util/perms.cf",
              "@(cryptoo:control.guest_inputs)"
            };

}

bundle agent common_guest(root, type, role, arch)

{

  classes:

    any::

      "have_systemctl"
        expression => fileexists("$(root)$(file_names.systemd_app)");

      "bootstrap"
        not => strcmp("$(this.root)", "/");

      "$(this.type)_guest"
        expression => "any";

      "have_master_ssh_key"
        expression => fileexists("/$(file_names.master_ssh_key)");

  files:

    any::

      "$(root)$(directory_groups.guest_system)/."
        perms => system,
        create => "true",
        handle => "$(root)$(directory_groups.guest_system)";

      "$(root)$(file_names.serial_autologin)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[serial_autologin])",
        classes => default:if_repaired("daemon_reload");

      "$(root)$(file_names.fstab)"
        perms => system,
        create => "true",
        edit_template => "/$(cryptoo:templates.template[fstab])",
        classes => default:if_repaired("fstab_repaired"),
        comment => "configure /etc/fstab";

    have_master_ssh_key::

      "$(root)$(file_names.authorized_keys)"
        perms => root,
        move_obstructions => "true",
        copy_from => default:local_cp("/$(cryptoo:file_names.master_ssh_key)");

    !bootstrap::
      "$(root)$(directory_groups.guest_system_private)/."
        perms => root,
        create => "true",
        handle => "$(root)$(directory_groups.guest_system_private)";

    bootstrap::
      "$(root)$(directory_groups.common_cryptoo_private)/."
        perms => root,
        create => "true",
        handle => "$(root)$(directory_groups.guest_system_private)";

  methods:

    any::

      "configure systemd"
        usebundle => systemd($(this.root), $(this.role));

    fstab_repaired&(!bootstrap)::

      "activate all mountpoints"
        usebundle => mount_all;

  commands:

    daemon_reload&have_systemctl&(!bootstrap)::

      "/$(file_names.systemd_app)"
        args => "daemon-reload",
        contain => basic_chroot($(root));

  reports:

    bootstrap::

      "Bootstrapping guest in $(this.root)";

}
