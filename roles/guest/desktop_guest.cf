body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/common/common.cf",
              "cryptoo/config/config.cf",
              "cryptoo/config/directory_names.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/config/templates.cf",
              "cryptoo/util/classes.cf",
              "cryptoo/util/perms.cf",
              "@(cryptoo:control.desktop_guest_inputs)"
            };

}

bundle agent desktop_guest(root, role, arch, multilib)

{

  vars:

    any::

      "xpra_conf[clipboard]"
        string => "yes";
      "xpra_conf[notifications]"
        string => "yes";
      "xpra_conf[tray]"
        string => "yes";
      "xpra_conf[system-tray]"
        string => "yes";
      "xpra_conf[speaker]"
        string => "on";
      "xpra_conf[microphone]"
        string => "off";
      "xpra_conf[encodings]"
        string => "all";
      "xpra_conf[video-encoders]"
        string => "all";
      "xpra_conf[sound-source]"
        string => "pulse";
      "xpra_conf[speaker-codec]"
        string => "vorbis";
      "xpra_conf[microphone-codec]"
        string => "vorbis";
      "xpra_conf[keyboard-sync]"
        string => "no";
      "xpra_conf[exit-with-children]"
        string => "yes";
      "xpra_conf[start-new-commands]"
        string => "yes";
      "xpra_conf[mdns]"
        string => "no";
      "xpra_conf[dbus-proxy]"
        string => "yes";
      "xpra_conf[input-method]"
        string => "none";
      "xpra_conf[pulseaudio]"
        string => "yes";

      "important_directories"
        slist =>
          {
            ".cache",
            ".config/pulse",
            ".dbus/session-bus",
            ".xpra",
            ".ssh"
          };

  classes:

    any::

      "have_xpra_config"
        expression => fileexists("$(this.root)$(file_names.xpra_config)");

  files:

    any::

      "$(this.root)$(directory_names.cryptoo_home)/."
        perms => cryptoo,
        create => "true";

      "$(this.root)$(directory_names.cryptoo_home)/$(important_directories)/."
        perms => cryptoo,
        create => "true";

      "$(this.root)$(directory_names.config_xpra)/."
        perms => system,
        create => "true";

      "$(this.root)$(file_names.xpra_service)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[ini])",
        template_method => "mustache",
        template_data => mergedata("cryptoo:config.systemd_services[xpra]");

    have_xpra_config::

      "$(this.root)$(file_names.xpra_config)"
        create => "false",
        edit_line => default:set_line_based("cryptoo:desktop_guest.xpra_conf", " = ", "\s*=\s*", ".*", "\s*#\s*");

  methods:

    any::

      "manage common files"
        usebundle => cryptoo_filesystem("$(root)", "$(role)", "$(arch)", "$(multilib)");

      "manage guest files"
        usebundle => common_guest("$(root)", "desktop", "$(role)", "$(arch)");

  commands:

    any::

      "/$(file_names.chown_app)"
        args => "-R cryptoo:cryptoo /$(directory_names.cryptoo_home)",
        classes => always_success,
        contain => silent_chroot($(root));

}
