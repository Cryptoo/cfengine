body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/config/config.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/config/network.cf"
            };

}

bundle agent dns

{

  meta:

    "tags" slist => { "cryptoo_role" };

  vars:

    any::

      "dnsmasq"
        string => '
          {
            "resolv": "$(file_names.dns_config_dnsmasq)",
            "tor": "192.168.$(network.subnet_config[tor]).$(config.defined_numbers[tor])",
            "i2p": "192.168.$(network.subnet_config[i2p]).$(config.defined_numbers[i2p])",
            "listen": "192.168.$(network.subnet_config[vpndmz]).$(config.defined_numbers[dns])"
          }
        ';

      "dns_external"
        string => '
          {
            "nameservers":
               [
                 { "nameserver": "$(config.dns[primary])" },
                 { "nameserver": "$(config.dns[backup])" }
               ]
          }
        ';

      "dns_internal"
        string => '
          {
            "nameservers":
               [
                 { "nameserver": "192.168.$(network.subnet_config[vpndmz]).$(config.defined_numbers[dns])" }
               ]
          }
        ';

  files:

    any::

      "/$(file_names.dnsmasq_conf)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[dnsmasq])",
        template_method => "mustache",
        template_data => parsejson($(this.dnsmasq));

      "/$(file_names.dns_config)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[resolv])",
        template_method => "mustache",
        template_data => parsejson($(this.dns_internal));

      "/$(file_names.dns_config_dnsmasq)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[resolv])",
        template_method => "mustache",
        template_data => parsejson($(this.dns_external));

  methods:

    any::

      "configure system as a DNS server"
        usebundle => server_guest("/", "dns", "x86_64", "no_multilib");

}
