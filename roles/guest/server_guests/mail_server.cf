body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/config/directory_names.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/config/site.cf",
              "cryptoo/config/templates.cf",
              "cryptoo/util/perms.cf"
            };

}

bundle agent mail_server(root)

{

  meta:

    "tags" slist => { "cryptoo_role" };

  vars:

    any::

      "dovecot[protocols]"
        string => "imap sieve";

      "auth[disable_plaintext_auth]"
        string => "yes";

      "mail[mail_location]"
        string => "maildir:/var/spool/mail/%u";

      "imap[mail_max_userip_connections]"
        string => "64";

      "ssl[ssl]"
        string => "required";
      "ssl[ssl_cipher_list]"
        string => "EECDH+ECDSA+AESGCM:EECDH+aRSA+AESGCM:EECDH+ECDSA+SHA384:EECDH+ECDSA+SHA256:EECDH+aRSA+SHA384:EECDH+aRSA+SHA256:EECDH+aRSA+RC4:EECDH:EDH+aRSA:!aNULL:!eNULL:!LOW:!3DES:!MD5:!EXP:!PSK:!SRP:!DSS:!RC4";
      "ssl[ssl_prefer_server_ciphers]"
        string => "yes";
      "ssl[ssl_protocols]"
        string => "!SSLv2 !SSLv3";
      "ssl[ssl_cert]"
        string => "</$(file_names.mail_server_crt)";
      "ssl[ssl_key]"
        string => "</$(file_names.mail_server_key)";
      "ssl[ssl_ca]"
        string => "</$(file_names.ca_crt)";

      "postfix_tls[smtp.gmail.com]"
        string => "encrypt";

  classes:

    any::

      "bootstrap"
        not => strcmp("$(this.root)", "/");

      "have_mail_config"
        expression => fileexists("$(this.root)$(file_names.mail_server_config)");

      "regen_tls"
        expression => makerule("$(this.root)$(file_names.postfix_tls).db", "$(this.root)$(file_names.postfix_tls)");

      "regen_transports"
        expression => makerule("$(this.root)$(file_names.postfix_transports).db", "$(this.root)$(file_names.postfix_transports)");

      "regen_header_checks"
        expression => makerule("$(this.root)$(file_names.postfix_header_checks).db", "$(this.root)$(file_names.postfix_header_checks)");

  files:

    any::

      "$(this.root)$(directory_names.mail_spool)/."
        perms => mail_spool,
        create => "true";

      "$(this.root)$(directory_names.config_dovecot_base)/."
        perms => system,
        create => "true";

      "$(this.root)$(directory_names.config_dovecot)/."
        perms => system,
        create => "true";

      "$(this.root)$(file_names.dovecot_conf)"
        perms => system,
        edit_line => default:set_line_based("cryptoo:mail_server.dovecot", " = ", "\s*=\s*", ".*", "\s*#\s*"),
        classes => restart_if_repaired("dovecot");

      "$(this.root)$(file_names.dovecot_auth)"
        perms => system,
        edit_line => default:set_line_based("cryptoo:mail_server.auth", " = ", "\s*=\s*", ".*", "\s*#\s*"),
        classes => restart_if_repaired("dovecot");

      "$(this.root)$(file_names.dovecot_mail)"
        perms => system,
        edit_line => default:set_line_based("cryptoo:mail_server.mail", " = ", "\s*=\s*", ".*", "\s*#\s*"),
        classes => restart_if_repaired("dovecot");

      "$(this.root)$(file_names.dovecot_imap)"
        perms => system,
        edit_line => default:set_line_based("cryptoo:mail_server.imap", " = ", "\s*=\s*", ".*", "\s*#\s*"),
        classes => restart_if_repaired("dovecot");

      "$(this.root)$(file_names.dovecot_ssl)"
        perms => system,
        edit_line => default:set_line_based("cryptoo:mail_server.ssl", " = ", "\s*=\s*", ".*", "\s*#\s*"),
        classes => restart_if_repaired("dovecot");

      "$(this.root)$(file_names.dovecot_master)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[dovecot_master])",
        classes => restart_if_repaired("dovecot");

      "$(this.root)$(directory_names.config_postfix)/."
        perms => system,
        create => "true";

      "$(this.root)$(file_names.postfix_tls)"
        perms => system,
        create => "true",
        edit_line => default:set_line_based("cryptoo:mail_server.postfix_tls", " ", "\s*", ".*", "\s*#\s*"),
        classes => restart_if_repaired("postfix");

      "$(this.root)$(file_names.postfix_transports)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[postfix_transport])",
        classes => restart_if_repaired("postfix");

      "$(this.root)$(file_names.postfix_header_checks)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[postfix_header])",
        classes => restart_if_repaired("postfix");

  methods:

    have_mail_config::

      "configure fetchmail"
        usebundle => fetchmail("$(this.root)");

    any::

      "configure system as a mail server"
        usebundle => server_guest("$(this.root)", "mail_server", "x86_64", "no_multilib");

  commands:

    regen_tls&(!bootstrap)::

      "/$(file_names.postmap_app)"
        args => "$(this.root)$(file_names.postfix_tls)",
        contain => silent;

    regen_transports&(!bootstrap)::

      "/$(file_names.postmap_app)"
        args => "$(this.root)$(file_names.postfix_transports)",
        contain => silent;

    regen_header_checks&(!bootstrap)::

      "/$(file_names.postmap_app)"
        args => "$(this.root)$(file_names.postfix_header_checks)",
        contain => silent;

}
