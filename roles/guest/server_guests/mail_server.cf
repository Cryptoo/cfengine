body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/config/directory_names.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/config/site.cf",
              "cryptoo/config/templates.cf",
              "cryptoo/util/perms.cf"
            };

}

bundle agent mail_server(root)

{

  meta:

    "tags" slist => { "cryptoo_role" };

  vars:

    any::

      "dovecot[protocols]"
        string => "imap sieve";

      "auth[disable_plaintext_auth]"
        string => "yes";

      "mail[mail_location]"
        string => "maildir:/var/spool/mail/%u";

      "imap[mail_max_userip_connections]"
        string => "64";

      "ssl[ssl]"
        string => "required";
      "ssl[ssl_cipher_list]"
        string => "EECDH+ECDSA+AESGCM:EECDH+aRSA+AESGCM:EECDH+ECDSA+SHA384:EECDH+ECDSA+SHA256:EECDH+aRSA+SHA384:EECDH+aRSA+SHA256:EECDH+aRSA+RC4:EECDH:EDH+aRSA:!aNULL:!eNULL:!LOW:!3DES:!MD5:!EXP:!PSK:!SRP:!DSS:!RC4";
      "ssl[ssl_prefer_server_ciphers]"
        string => "yes";
      "ssl[ssl_protocols]"
        string => "!SSLv2 !SSLv3";
      "ssl[ssl_cert]"
        string => "</$(file_names.mail_server_crt)";
      "ssl[ssl_key]"
        string => "</$(file_names.mail_server_key)";
      "ssl[ssl_ca]"
        string => "</$(file_names.ca_crt)";

      "postfix[myhostname]"
        string => "mail.$(site.config[ddns])";
      "postfix[mydomain]"
        string => "$(site.config[ddns])";
      "postfix[myorigin]"
        string => "$mydomain";
      "postfix[mydestination]"
        string => "$myhostname, localhost.$mydomain, localhost, $mydomain, localhost.local";
      "postfix[mailbox_command]"
        string => '/usr/libexec/dovecot/dovecot-lda -f "$SENDER" -a "$RECIPIENT"';
      "postfix[transport_maps]"
        string => "hash:/etc/postfix/transport";
      "postfix[smtp_generic_maps]"
        string => "hash:/etc/postfix/default_outgoing_map";
      "postfix[sender_dependent_default_transport_maps]"
        string => "hash:/etc/postfix/sender_map";
      "postfix[smtp_sasl_auth_enable]"
        string => "yes";
      "postfix[smtp_sasl_password_maps]"
        string => "hash:/etc/postfix/saslpass_default";
      "postfix[smtp_sasl_security_options]"
        string => "noanonymous";
      "postfix[smtp_use_tls]"
        string => "yes";
      "postfix[smtp_connect_timeout]"
        string => "180s";
      "postfix[smtp_tls_key_file]"
        string => "/$(file_names.mail_server_key)";
      "postfix[smtp_tls_cert_file]"
        string => "/$(file_names.mail_server_crt)";
      "postfix[smtp_tls_security_level]"
        string => "may";
      "postfix[smtp_tls_policy_maps]"
        string => "hash:/etc/postfix/tls_policy";
      "postfix[smtp_tls_loglevel]"
        string => "0";
      "postfix[tls_random_source]"
        string => "dev:/dev/urandom";
      "postfix[message_size_limit]"
        string => "1024000000";
      "postfix[mailbox_size_limit]"
        string => "102400000000";
      "postfix[smtpd_sasl_auth_enable]"
        string => "yes";
      "postfix[smtpd_sasl_type]"
        string => "dovecot";
      "postfix[smtpd_sasl_path]"
        string => "private/auth";
      "postfix[smtpd_recipient_restrictions]"
        string => "permit_mynetworks, permit_sasl_authenticated, reject_unauth_destination";
      "postfix[smtpd_use_tls]"
        string => "yes";
      "postfix[smtpd_tls_key_file]"
        string => "/$(file_names.mail_server_key)";
      "postfix[smtpd_tls_cert_file]"
        string => "/$(file_names.mail_server_crt)";
      "postfix[smtpd_tls_loglevel]"
        string => "1";
      "postfix[smtpd_tls_received_header]"
        string => "yes";
      "postfix[smtpd_tls_session_cache_timeout]"
        string => "3600";
      "postfix[smtpd_tls_auth_only]"
        string => "yes";
      "postfix[smtpd_tls_loglevel]"
        string => "0";

  files:

    any::

      "$(this.root)$(directory_names.mail_spool)/."
        perms => mail_spool,
        create => "true";

      "$(this.root)$(directory_names.config_dovecot_base)/."
        perms => system,
        create => "true";

      "$(this.root)$(directory_names.config_dovecot)/."
        perms => system,
        create => "true";

      "$(this.root)$(file_names.dovecot_conf)"
        perms => system,
        edit_line => default:set_line_based("cryptoo:mail_server.dovecot", " = ", "\s*=\s*", ".*", "\s*#\s*"),
        classes => default:if_repaired("reload_dovecot");

      "$(this.root)$(file_names.dovecot_auth)"
        perms => system,
        edit_line => default:set_line_based("cryptoo:mail_server.auth", " = ", "\s*=\s*", ".*", "\s*#\s*"),
        classes => default:if_repaired("reload_dovecot");

      "$(this.root)$(file_names.dovecot_mail)"
        perms => system,
        edit_line => default:set_line_based("cryptoo:mail_server.mail", " = ", "\s*=\s*", ".*", "\s*#\s*"),
        classes => default:if_repaired("reload_dovecot");

      "$(this.root)$(file_names.dovecot_imap)"
        perms => system,
        edit_line => default:set_line_based("cryptoo:mail_server.imap", " = ", "\s*=\s*", ".*", "\s*#\s*"),
        classes => default:if_repaired("reload_dovecot");

      "$(this.root)$(file_names.dovecot_ssl)"
        perms => system,
        edit_line => default:set_line_based("cryptoo:mail_server.ssl", " = ", "\s*=\s*", ".*", "\s*#\s*"),
        classes => default:if_repaired("reload_dovecot");

      "$(this.root)$(file_names.dovecot_master)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[dovecot_master])",
        classes => default:if_repaired("reload_dovecot");

      "$(this.root)$(directory_names.config_postfix)/."
        perms => system,
        create => "true";

      "$(this.root)$(file_names.postfix_main)"
        perms => system,
        edit_line => default:set_line_based("cryptoo:mail_server.postfix", " = ", "\s*=\s*", ".*", "\s*#\s*"),
        classes => default:if_repaired("reload_postfix");

  methods:

    any::

      "configure system as a mail server"
        usebundle => server_guest("$(this.root)", "mail_server", "x86_64", "no_multilib");

  commands:

    reload_dovecot::

      "/$(file_names.systemd_app)"
        args => "restart dovecot",
        contain => silent;

    reload_postfix::

      "/$(file_names.systemd_app)"
        args => "restart postfix",
        contain => silent;

}
