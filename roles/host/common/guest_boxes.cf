body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/config/config.cf",
              "cryptoo/config/directory_names.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/roles/host/common/guest_role.cf",
              "cryptoo/util/perms.cf",
              "cryptoo/util/setfattr.cf"
            };

}

bundle agent guest_boxes

{

  classes:

    any::

      "have_setfattr_app"
        expression => fileexists("/$(file_names.setfattr_app)");

  files:

    any::

      "/$(directory_names.fs_config_guest_inboxes)/$(config.server_guests)/."
        perms => root_qemu,
        create => "true",
        comment => "guest inboxes";

      "/$(directory_names.fs_config_guest_inboxes)/$(config.server_guests)/site_config.json"
        perms => root_qemu,
        move_obstructions => "true",
        copy_from => default:local_cp("/$(cryptoo:file_names.site_config)"),
        comment => "guest site config";

      "/$(directory_names.fs_config_guest_inboxes)/$(config.server_guests)/machine_names.json"
        perms => root_qemu,
        move_obstructions => "true",
        copy_from => default:local_cp("/$(cryptoo:file_names.machine_names)"),
        comment => "guest machine names";

      "/$(directory_names.fs_config_guest_inboxes)/$(config.server_guests)/subnet_config.json"
        perms => root_qemu,
        move_obstructions => "true",
        copy_from => default:local_cp("/$(cryptoo:file_names.subnet_config)"),
        comment => "guest subnet config";

      "/$(directory_names.fs_config_guest_outboxes)/$(config.server_guests)/."
        perms => root_qemu,
        create => "true",
        comment => "guest outboxes";

  methods:

    any::

      "configure guest roles"
        usebundle => guest_role("/$(directory_names.fs_config_guest_inboxes)/$(config.server_guests)", "$(config.server_guests)");

    have_setfattr_app::

      "set virtfs uid on guest inboxes"
        usebundle => setfattr("user.virtfs.uid", "$(config.virtfs[uid_gid][0])", "/$(directory_names.fs_config_guest_inboxes)", "$(config.server_guests)");

      "set virtfs gid on guest inboxes"
        usebundle => setfattr("user.virtfs.gid", "$(config.virtfs[uid_gid][0])", "/$(directory_names.fs_config_guest_inboxes)", "$(config.server_guests)");

      "set virtfs permissions on guest inboxes"
        usebundle => setfattr("user.virtfs.mode", "$(config.virtfs[mode][dir][0700])", "/$(directory_names.fs_config_guest_inboxes)", "$(config.server_guests)");

  commands:

    have_cryptoo::

      "/$(file_names.cryptoo_perms_app)";

}