body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/config/config.cf",
              "cryptoo/config/directory_names.cf",
              "cryptoo/config/network.cf",
              "cryptoo/config/templates.cf",
              "cryptoo/util/perms.cf",
              "@(cryptoo:control.systemd_proto_inputs)"
            };

}

bundle agent network_file(root, role, name, number, type, params)

{

  vars:

    any::

      "defined_numbers"
        data => mergedata("cryptoo:config.defined_numbers");

      "subnet_config"
        data => mergedata("cryptoo:network.subnet_config");

      "extra_route_list"
        slist => {},
        ifvarclass => "!extra_routes_obtained_$(this.name)";

      "primary_route_list"
        slist => {},
        ifvarclass => "!routes_obtained_$(this.name)";

      "desktop_routes_$(this.name)"
        string => "",
        ifvarclass => "routes_evaluated_$(this.name)&!desktop_routes_$(this.name)";

      "desktop_routes_$(this.name)"
        string => ',
                 { "section": "Route",
                   "values":
                     [
                       { "LHS": "Gateway", "RHS": "192.168.$(subnet_config[lan]).$(defined_numbers[desktop_host])" },
                       { "LHS": "Destination", "RHS": "192.168.$(subnet_config[lan]).64/27" }
                     ]
                 }',
        ifvarclass => "desktop_routes_$(this.name)";

    any::

      "network_params_$(this.name)"
        data => parsejson("$(this.params)");

      "routes_$(this.name)"
        string => "",
        ifvarclass => "routes_evaluated_$(this.name)&!have_routes_$(this.name)&!extra_routes_$(this.name)",
        classes => default:if_ok("routes_ready");

      "primary_route_list"
        slist => getvalues("route_json_$(this.name)"),
        ifvarclass => "routes_obtained_$(this.name)";

      "extra_route_list"
        slist => getvalues("extra_route_json_$(this.name)"),
        ifvarclass => "extra_routes_obtained_$(this.name)";

      "route_list"
        slist => { @(primary_route_list), @(extra_route_list) },
        ifvarclass => "routes_obtained_$(this.name)|extra_routes_obtained_$(this.name)",
        classes => default:if_ok("route_list_ready_$(this.name)");

      "route_list_joined"
        string => join(", ", "route_list"),
        ifvarclass => "route_list_ready_$(this.name)",
        classes => default:if_ok("route_list_joined_$(this.name)");

      "routes_$(this.name)"
        string => ", $(route_list_joined)",
        ifvarclass => "route_list_joined_$(this.name)",
        classes => default:if_ok("routes_ready");

    type_no_address::

      "file_name_$(this.name)"
        string => "$(this.root)$(directory_names.config_network)/$(this.name).network";

      "device_$(this.name)"
        string => "$(network_params_$(this.name)[device])";

      "network_$(this.name)"
        string => '
            {
              "sections":
                 [
                   { "section": "Match",
                     "values":
                       [
                         { "LHS": "Name", "RHS": "$(device_$(this.name))" }
                       ]
                   },
                   { "section": "Network",
                     "values":
                       [
                         { "LHS": "DHCP", "RHS": "false" },
                         { "LHS": "LinkLocalAddressing", "RHS": "No"}
                       ]
                   }
                 ]
            }',
        classes => default:if_ok("network_json_ready");

    type_tun::

      "file_name_$(this.name)"
        string => "$(this.root)$(directory_names.config_network)/$(this.name).network";

      "device_$(this.name)"
        string => "$(network_params_$(this.name)[device])";

      "forward_$(this.name)"
        string => "$(network_params_$(this.name)[ipforward])";

      "network_$(this.name)"
        string => '
            {
              "sections":
                 [
                   { "section": "Match",
                     "values":
                       [
                         { "LHS": "Name", "RHS": "$(device_$(this.name))" }
                       ]
                   },
                   { "section": "Network",
                     "values":
                       [
                         { "LHS": "DHCP", "RHS": "false" },
                         { "LHS": "Address", "RHS": "192.168.$(network.subnet_config[lan]).$(network_params_$(this.name)[number])/27" },
                         { "LHS": "IPForward", "RHS": "$(forward_$(this.name))" },
                         { "LHS": "LinkLocalAddressing", "RHS": "No"}
                       ]
                   }
                 ]
            }
          ',
        classes => default:if_ok("network_json_ready");

    type_tap::

      "bridge_$(this.name)"
        string => "$(network_params_$(this.name)[bridge])";

      "file_name_$(this.name)"
        string => "$(this.root)$(directory_names.config_network)/$(this.name).network";

      "device_$(this.name)"
        string => "$(network_params_$(this.name)[device])";

      "network_$(this.name)"
        string => '
            {
              "sections":
                 [
                   { "section": "Match",
                     "values":
                       [
                         { "LHS": "Name", "RHS": "$(device_$(this.name))" }
                       ]
                   },
                   { "section": "Network",
                     "values":
                       [
                         { "LHS": "DHCP", "RHS": "false" },
                         { "LHS": "Bridge", "RHS": "$(bridge_$(this.name))" },
                         { "LHS": "LinkLocalAddressing", "RHS": "No"}
                       ]
                   }
                 ]
            }
          ',
        classes => default:if_ok("network_json_ready");

    type_bridge_member::

      "bridge_$(this.name)"
        string => "$(network_params_$(this.name)[bridge])";

      "file_name_$(this.name)"
        string => "$(this.root)$(directory_names.config_network)/$(bridge_$(this.name))$(this.number).network";

      "network_$(this.name)"
        string => '
          {
            "sections":
               [
                 { "section": "Match",
                   "values":
                     [
                       { "LHS": "Name", "RHS": "$(bridge_$(this.name))$(this.number)" }
                     ]
                 },
                 { "section": "Network",
                   "values":
                     [
                       { "LHS": "DHCP", "RHS": "false" },
                       { "LHS": "Bridge", "RHS": "$(bridge_$(this.name))" },
                       { "LHS": "LinkLocalAddressing", "RHS": "No"}
                     ]
                 }
               ]
          }
        ',
        classes => default:if_ok("network_json_ready");

    type_physical::

      "bridge_$(this.name)"
        string => "$(network_params_$(this.name)[bridge])";

      "device_$(this.name)"
        string => "$(network_params_$(this.name)[device])";

      "file_name_$(this.name)"
        string => "$(this.root)$(directory_names.config_network)/$(bridge_$(this.name))_physical.network";

      "network_$(this.name)"
        string => '
          {
            "sections":
               [
                 { "section": "Match",
                   "values":
                     [
                       { "LHS": "Name", "RHS": "$(device_$(this.name))" }
                     ]
                 },
                 { "section": "Network",
                   "values":
                     [
                       { "LHS": "DHCP", "RHS": "false" },
                       { "LHS": "Bridge", "RHS": "$(bridge_$(this.name))" },
                       { "LHS": "LinkLocalAddressing", "RHS": "No"}
                     ]
                 }
               ]
          }
        ',
        classes => default:if_ok("network_json_ready");

    type_gateway&routes_ready::

      "zone_$(this.name)"
        string => "$(network_params_$(this.name)[zone])";

      "gateway_$(this.name)"
        string => "$(network_params_$(this.name)[gateway])";

      "forward_$(this.name)"
        string => "$(network_params_$(this.name)[ipforward])";

      "file_name_$(this.name)"
        string => "$(this.root)$(directory_names.config_network)/$(zone_$(this.name)).network";

      "network_$(this.name)"
        string => '
          {
            "sections":
               [
                 { "section": "Match",
                   "values":
                     [
                       { "LHS": "Name", "RHS": "$(this.name)" }
                     ]
                 },
                 { "section": "Network",
                   "values":
                     [
                       { "LHS": "DHCP", "RHS": "false" },
                       { "LHS": "Address", "RHS": "192.168.$(network.subnet_config[$(zone_$(this.name))]).$(this.number)/24" },
                       { "LHS": "Gateway", "RHS": "192.168.$(network.subnet_config[$(zone_$(this.name))]).$(config.defined_numbers[$(gateway_$(this.name))])" },
                       { "LHS": "IPForward", "RHS": "$(forward_$(this.name))" },
                       { "LHS": "LinkLocalAddressing", "RHS": "No"}
                     ]
                 } $(desktop_routes_$(this.name))
                   $(routes_$(this.name))
               ]
          }
        ',
        classes => default:if_ok("network_json_ready");

    type_restricted_gateway&routes_ready::

      "zone_$(this.name)"
        string => "$(network_params_$(this.name)[zone])";

      "gateway_$(this.name)"
        string => "$(network_params_$(this.name)[gateway])";

      "forward_$(this.name)"
        string => "$(network_params_$(this.name)[ipforward])";

      "file_name_$(this.name)"
        string => "$(this.root)$(directory_names.config_network)/$(zone_$(this.name)).network";

      "network_$(this.name)"
        string => '
          {
            "sections":
               [
                 { "section": "Match",
                   "values":
                     [
                       { "LHS": "Name", "RHS": "$(this.name)" }
                     ]
                 },
                 { "section": "Network",
                   "values":
                     [
                       { "LHS": "DHCP", "RHS": "false" },
                       { "LHS": "Address", "RHS": "192.168.$(network.subnet_config[$(zone_$(this.name))]).$(this.number)/27" },
                       { "LHS": "Gateway", "RHS": "192.168.$(network.subnet_config[$(zone_$(this.name))]).$(config.defined_numbers[$(gateway_$(this.name))])" },
                       { "LHS": "IPForward", "RHS": "$(forward_$(this.name))" },
                       { "LHS": "LinkLocalAddressing", "RHS": "No"}
                     ]
                 } $(routes_$(this.name))
               ]
          }
        ',
        classes => default:if_ok("network_json_ready");

    type_simple&routes_ready::

      "zone_$(this.name)"
        string => "$(network_params_$(this.name)[zone])";

      "forward_$(this.name)"
        string => "$(network_params_$(this.name)[ipforward])";

      "file_name_$(this.name)"
        string => "$(this.root)$(directory_names.config_network)/$(zone_$(this.name)).network";

      "network_$(this.name)"
        string => '
          {
            "sections":
               [
                 { "section": "Match",
                   "values":
                     [
                       { "LHS": "Name", "RHS": "$(this.name)" }
                     ]
                 },
                 { "section": "Network",
                   "values":
                     [
                       { "LHS": "DHCP", "RHS": "false" },
                       { "LHS": "Address", "RHS": "192.168.$(network.subnet_config[$(zone_$(this.name))]).$(this.number)/24" },
                       { "LHS": "IPForward", "RHS": "$(forward_$(this.name))" },
                       { "LHS": "LinkLocalAddressing", "RHS": "No"}
                     ]
                 } $(desktop_routes_$(this.name))
                   $(routes_$(this.name))

               ]
          }
        ',
        classes => default:if_ok("network_json_ready");

    type_dhcp&routes_ready::

      "zone_$(this.name)"
        string => "$(network_params_$(this.name)[zone])";

      "file_name_$(this.name)"
        string => "$(this.root)$(directory_names.config_network)/$(zone_$(this.name)).network";

      "forward_$(this.name)"
        string => "$(network_params_$(this.name)[ipforward])";

      "network_$(this.name)"
        string => '
          {
            "sections":
               [
                 { "section": "Match",
                   "values":
                     [
                       { "LHS": "Name", "RHS": "$(this.name)" }
                     ]
                 },
                 { "section": "Network",
                   "values":
                     [
                       { "LHS": "DHCP", "RHS": "true" },
                       { "LHS": "IPForward", "RHS": "$(forward_$(this.name))" },
                       { "LHS": "LinkLocalAddressing", "RHS": "No"}
                     ]
                 } $(routes_$(this.name))
               ]
          }
        ',
        classes => default:if_ok("network_json_ready");

    !skip&network_json_ready::

      "final_json"
        string => "$(network_$(this.name))",
        classes => default:if_ok("final_json");

  classes:

    any::

      "skip"
        or => { strcmp("$(this.name)", "none"), strcmp("$(this.type)", "virtual") };

      "network_$(role)"
        expression => "any";

      "have_routes_$(this.name)"
        expression => regextract('(?s).*("routes":.*{.*?})', "$(this.params)", "route_regex");

      "routes_evaluated_$(this.name)"
        expression => "any";

    network_firewall_internal&routes_evaluated_eth0::

      "extra_routes_$(this.name)"
        expression => "any";

    network_firewall_internal&routes_evaluated_eth3::

      "desktop_routes_$(this.name)"
        expression => "any";

    network_master&routes_evaluated_lantap::

      "desktop_routes_$(this.name)"
        expression => "any";

    !skip::

      "type_$(this.type)"
        expression => "any";

  methods:

    any::

      "generate json for routes"
        usebundle => networkd_routes("$(zone_$(this.name))", "{ $(this.route_regex[1]) }"),
        ifvarclass => "have_routes_$(this.name)",
        useresult => "route_json_$(this.name)",
        classes => default:if_ok("routes_obtained_$(this.name)");

      "generate json for supplemental routes"
        usebundle => vpn_secondary_routes(),
        ifvarclass => "extra_routes_$(this.name)",
        useresult => "extra_route_json_$(this.name)",
        classes => default:if_ok("extra_routes_obtained_$(this.name)");

  files:

    final_json&!skip::

      "$(file_name_$(this.name))"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[ini])",
        template_method => "mustache",
        template_data => parsejson("$(final_json)");
}
