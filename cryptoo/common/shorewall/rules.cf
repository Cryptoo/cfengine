body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/common/shorewall/get_port_info.cf",
              "@(cryptoo:control.shorewall_macro_inputs)",
              "@(cryptoo:control.shorewall_rules_inputs)"
            };

}

bundle agent shorewall_rules(root, role)

{

  vars:

    any::

      "shorewall_macros"
        slist => bundlesmatching("cryptoo:shorewall_macro_.*", "shorewall_macro");

      "role_macros"
        slist => bundlesmatching("cryptoo:shorewall_macro_.*", "$(this.role)");

      "relevant_macros"
        slist => intersection(shorewall_macros, role_macros);

      "shorewall_rules"
        slist => bundlesmatching("cryptoo:shorewall_rules_.*", "shorewall_rules");

      "role_rules"
        slist => bundlesmatching("cryptoo:shorewall_rules_.*", "$(this.role)");

      "relevant_rules"
        slist => intersection(shorewall_rules, role_rules);

  classes:

    any::

      "have_relevant_macros"
        expression => isgreaterthan(length("relevant_macros"), "0");

      "have_relevant_rules"
        expression => isgreaterthan(length("relevant_rules"), "0");

  methods:

    any::

      "get port info"
        usebundle => get_port_info("$(this.root)", "$(this.role)"),
        useresult => "port_info",
        classes => default:if_ok("have_port_info");

    have_port_info::

      "configure shorewall macros"
        usebundle => $(relevant_macros)("$(this.root)", "$(port_info[port_info])"),
        ifvarclass => "have_relevant_macros";

      "configure /etc/shorewall/rules"
        usebundle => $(relevant_rules)("$(this.root)"),
        ifvarclass => "have_relevant_rules";

}
