body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/common/shorewall/bridge_child_interfaces.cf",
              "cryptoo/common/shorewall/bridge_extra_children.cf",
              "cryptoo/config/config.cf"
            };

}

bundle agent bridge_children(role, zone)

{

  vars:

    any::

      "firewall"
        data => mergedata("cryptoo:config.firewall");

      "zone_name"
        string => "$(firewall[zone][$(zone)][name])";

      "prefix"
        string => "$(firewall[zone][$(zone)][prefix])";

      "options"
        string => "$(firewall[interface_options][member])";

      "children"
        slist => getindices("children_$(this.zone)"),
        ifvarclass => "scanned_children_$(this.zone)",
        classes => default:if_ok("have_children_$(this.zone)");

      "bridge_children"
        slist => maplist('{ "zone_full": "$(prefix)$(this):$(zone_name)", "zone": "$(prefix)$(this)", "type": "bport", "interface": "$(child_interfaces[$(this)])", "interface_options": "$(options)" }', @(children) ),
        ifvarclass => "have_child_interfaces_$(this.zone)",
        classes => default:if_ok("have_bridge_children_$(this.zone)");

      "json"
        string => join(", ", "bridge_children"),
        ifvarclass => "have_bridge_children_$(this.zone)",
        classes => default:if_ok("have_json_$(this.zone)");

  methods:

    any::

      "get a list of children for this zone"
        usebundle => bridge_extra_children("$(this.role)", "$(this.zone)"),
        useresult => "children_$(this.zone)",
        classes => default:if_ok("scanned_children_$(this.zone)");

      "get the interface names for children of this bridge"
        usebundle => bridge_child_interfaces("$(zone)", "$(children)"),
        useresult => "child_interfaces",
        ifvarclass => "have_children_$(this.zone)",
        classes => default:if_ok("have_child_interfaces_$(this.zone)");

  reports:

    any::

      "$(json)"
        bundle_return_value_index => "$(zone)_json",
        ifvarclass => "have_json_$(this.zone)";

}
