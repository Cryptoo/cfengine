body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/config/site.cf"
            };

}

bundle agent extra_desktop_guests(host)

{

  vars:

    any::

      "extra_guest_data"
        data => mergedata("cryptoo:site.config[extra_guests]");

      "site_extra_guests"
        slist => getindices("extra_guest_data");

  classes:

    any::

      "have_site_extra_guests"
        expression => isgreaterthan(length("site_extra_guests"), "0");

    have_site_extra_guests::

      "host_$(site_extra_guests)"
        expression => strcmp("$(extra_guest_data[$(site_extra_guests)][host])", "$(this.host)");

      "desktop_guest_$(site_extra_guests)"
        expression => strcmp("$(extra_guest_data[$(site_extra_guests)][category])", "desktop_guest");

      "host_desktop_guest_$(site_extra_guests)"
        expression => "any",
        ifvarclass => "host_$(site_extra_guests)&desktop_guest_$(site_extra_guests)";

  reports:

    have_site_extra_guests::

      "$(extra_guest_data[$(site_extra_guests)][name])"
        bundle_return_value_index => "$(site_extra_guests)",
        ifvarclass => "host_desktop_guest_$(site_extra_guests)";

}
