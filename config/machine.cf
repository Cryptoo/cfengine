body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/cryptoo.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/config/network.cf"
            };

}

bundle common machine

{

  vars:

    defined_number::

      "machine_number"
        string => "$(config.defined_numbers[$(cryptoo.configured_role)])";

      "hostname"
        string => "$(network.machine_names[$(this.machine_number)][name])";

    have_hostname&(!defined_number)::

      "hostname"
        string => "$(cryptoo.configured_hostname)";

      "machine_number"
        string => "$(network.machine_numbers[$(this.hostname)])";

    cryptoo_host::

      "root_dev"
        string => "/dev/mapper/$(this.hostname)-root";

      "root_uuid"
        string => execresult("/$(file_names.blkid_app) -s UUID -o value $(root_dev)","noshell");

    !cryptoo_host::

      "root_uuid"
        string => execresult("/$(file_names.blkid_app) -s UUID -o value /dev/vda","noshell");

  classes:

    any::

      "defined_number"
        not => strcmp("$(config.defined_numbers[$(cryptoo.configured_role)])","");

  reports:

    verbose_mode&(defined_number|(have_hostname&(!defined_number)))::

      "My role is: $(cryptoo.configured_role)";
      "My number is: $(this.machine_number)";
      "My hostname is: $(this.hostname)";

}