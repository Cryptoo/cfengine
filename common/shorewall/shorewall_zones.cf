body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/common/shorewall/bridge.cf",
              "cryptoo/common/shorewall/bridge_children.cf",
              "cryptoo/config/config.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/config/templates.cf",
              "cryptoo/util/perms.cf"
            };

}

bundle agent shorewall_zones(root, role)

{

  vars:

    any::

      "basic_zones_ipv4"
        slist => getvalues("file_groups.firewall_zones[$(this.role)]");

      "bridge_zones_ipv4"
        slist => getvalues("file_groups.firewall_bridges[$(this.role)]");

    bridge_ipv4|no_bridge_ipv4::

      "zone[$(basic_zones_ipv4)][name]"
        string => '"zone_full": "$(basic_zones_ipv4)"';

      "zone[$(basic_zones_ipv4)][type]"
        string => '"type": "ipv4"';

      "zone_basic_ipv4_json[$(basic_zones_ipv4)]"
        string => '{ $(zone[$(basic_zones_ipv4)][name]), $(zone[$(basic_zones_ipv4)][type]) }';

      "zone_basic_ipv4_json_list"
        slist => getvalues("zone_basic_ipv4_json");

      "zone_basic_ipv4_json"
        string => join(", ", "zone_basic_ipv4_json_list");

    bridge_children_done::

      "bridge_zones"
        slist => getvalues("bridges");

      "bridge_zone_json"
        string => join(", ", "bridge_zones");

      "bridge_children"
        slist => getvalues("bridge_zone_child");

      "bridge_children_json"
        string => join(", ", "bridge_children");

      "zone_json"
        string => '{ "zones": [ $(zone_basic_ipv4_json), $(bridge_zone_json), $(bridge_children_json) ] }';

    no_bridge_ipv4::

      "zone_json"
        string => '{ "zones": [ $(zone_basic_ipv4_json) ] }';

  classes:

    any::

      "no_bridge_ipv4"
        not => isgreaterthan(length("bridge_zones_ipv4"), "0");

      "bridge_ipv4"
        expression => isgreaterthan(length("bridge_zones_ipv4"), "0");

  files:

    no_bridge_ipv4|bridge_children_done::

      "$(root)$(file_names.shorewall_zones)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[shorewall_zone])",
        template_method => "mustache",
        template_data => parsejson("$(zone_json)");

  methods:

    bridge_ipv4::

      "get list of bridge zones"
        usebundle => bridge("$(bridge_zones_ipv4)"),
        useresult => "bridges",
        handle => "have_bridges";

      "get list of children for bridge zones"
        usebundle => bridge_children("$(bridge_zones_ipv4)"),
        useresult => "bridge_zone_child",
        depends_on => { "have_bridges" },
        classes => default:if_ok("bridge_children_done");

}
