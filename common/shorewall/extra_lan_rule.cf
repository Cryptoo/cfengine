body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/config/config.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/config/network.cf",
              "cryptoo/config/site.cf",
              "cryptoo/config/templates.cf",
              "cryptoo/util/classes.cf",
              "cryptoo/util/perms.cf"
            };

}

bundle agent shorewall_extra_lan_rule(guest_number)

{

  vars:

    any::

      "defined_numbers"
        data => mergedata("cryptoo:config.defined_numbers");

      "subnet_config"
        data => mergedata("cryptoo:network.subnet_config");

      "extra_guest_data"
        data => mergedata("cryptoo:site.config[extra_guests]");

      "guest_role"
        string => "$(extra_guest_data[$(this.guest_number)][type])";

    !extra_lan_role_set::

      "extra_lan_role"
        string => "";

    extra_lan_role_undefined::

      "extra_lan_role"
        string => ', {
                     "action": "ACCEPT",
                     "source": "l$(this.guest_number):192.168.$(subnet_config[lan]).$(this.guest_number)",
                     "destination": "l$(defined_numbers[firewall_internal])"
                   }';

    extra_lan_role_irc_client::

      "extra_lan_role"
        string => ', {
                     "action": "IRC(ACCEPT)",
                     "source": "l$(this.guest_number):192.168.$(subnet_config[lan]).$(this.guest_number)",
                     "destination": "l$(defined_numbers[firewall_internal]):192.168.$(subnet_config[vpndmz]).$(defined_numbers[irc_bouncer])"
                   }';

    extra_lan_role_im::

      "extra_lan_role"
        string => ', {
                     "action": "IRC(ACCEPT)",
                     "source": "l$(this.guest_number):192.168.$(subnet_config[lan]).$(this.guest_number)",
                     "destination": "l$(defined_numbers[firewall_internal]):192.168.$(subnet_config[vpndmz]).$(defined_numbers[irc_bouncer])"
                   },
                   {
                     "action": "tor(ACCEPT)",
                     "source": "l$(this.guest_number):192.168.$(subnet_config[lan]).$(this.guest_number)",
                     "destination": "l$(defined_numbers[firewall_internal]):192.168.$(subnet_config[tor]).$(defined_numbers[tor])"
                   },
                   {
                     "action": "ACCEPT",
                     "source": "l$(this.guest_number):192.168.$(subnet_config[lan]).$(this.guest_number)",
                     "destination": "l$(defined_numbers[firewall_internal]):!+private"
                   },
                   {
                     "action": "ACCEPT",
                     "source": "l$(this.guest_number):192.168.$(subnet_config[lan]).$(this.guest_number)",
                     "destination": "l$(defined_numbers[firewall_internal]):$(config.transparent_proxy_net[tor])"
                   }';

    extra_lan_role_set::

      "guest_json"
        string => '{
                     "action": "SSH(ACCEPT)",
                     "source": "l$(defined_numbers[master]):192.168.$(subnet_config[lan]).$(defined_numbers[master])",
                     "destination": "l$(this.guest_number):192.168.$(subnet_config[lan]).$(this.guest_number)"
                   },
                   {
                     "action": "SSH(ACCEPT)",
                     "source": "l$(defined_numbers[vpn_server]):192.168.$(subnet_config[lan]).0/24",
                     "destination": "l$(this.guest_number):192.168.$(subnet_config[lan]).$(this.guest_number)"
                   },
                   {
                     "action": "SSH(ACCEPT)",
                     "source": "l0:192.168.$(subnet_config[lan]).0/24",
                     "destination": "l$(this.guest_number):192.168.$(subnet_config[lan]).$(this.guest_number)"
                   },
                   {
                     "action": "NTP(ACCEPT)",
                     "source": "l$(this.guest_number):192.168.$(subnet_config[lan]).$(this.guest_number)",
                     "destination": "l$(defined_numbers[firewall_internal]):!+private"
                   },
                   {
                     "action": "cfengine(ACCEPT)",
                     "source": "l$(this.guest_number):192.168.$(subnet_config[lan]).$(this.guest_number)",
                     "destination": "l$(defined_numbers[master]):192.168.$(subnet_config[lan]).$(defined_numbers[master])"
                   },
                   {
                     "action": "DNS(ACCEPT)",
                     "source": "l$(this.guest_number):192.168.$(subnet_config[lan]).$(this.guest_number)",
                     "destination": "l$(defined_numbers[firewall_internal]):192.168.$(subnet_config[vpndmz]).$(defined_numbers[dns])"
                   }$(extra_lan_role)',
        classes => default:if_ok("have_basic_guest_json");
  classes:

    any::

      "extra_lan_role_$(guest_role)"
        expression => "any";

      "extra_lan_role_set"
        expression => "any";

  reports:

    have_basic_guest_json::

      "$(guest_json)"
        bundle_return_value_index => "$(this.guest_number)";

}
