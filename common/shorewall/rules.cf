body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/config/file_names.cf",
              "@(cryptoo:control.shorewall_macro_inputs)",
              "@(cryptoo:control.shorewall_rules_inputs)"
            };

}

bundle agent shorewall_rules(root, role)

{

  vars:

    any::

      "shorewall_macros"
        slist => bundlesmatching("cryptoo:shorewall_macro_.*", "shorewall_macro");

      "role_macros"
        slist => bundlesmatching("cryptoo:shorewall_macro_.*", "$(this.role)");

      "relevant_macros"
        slist => intersection(shorewall_macros, role_macros);

      "shorewall_rules"
        slist => bundlesmatching("cryptoo:shorewall_rules_.*", "shorewall_rules");

      "role_rules"
        slist => bundlesmatching("cryptoo:shorewall_rules_.*", "$(this.role)");

      "relevant_rules"
        slist => intersection(shorewall_rules, role_rules);

  classes:

    any::

      "bootstrap"
        not => strcmp("$(this.root)", "/");

      "have_shorewall_app"
        expression => fileexists("$(this.root)$(file_names.shorewall_app)");

  methods:

    any::

      "configure shorewall macros"
        usebundle => $(relevant_macros)("$(this.root)"),
        classes => default:if_repaired("update_shorewall");

      "configure /etc/shorewall/rules"
        usebundle => $(relevant_rules)("$(this.root)"),
        classes => default:if_repaired("update_shorewall");

  commands:

    update_shorewall&have_shorewall_app&(!bootstrap)::

      "/$(file_names.shorewall_app)"
        args => "restart";

}
