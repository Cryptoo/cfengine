body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/config/file_names.cf",
              "cryptoo/config/templates.cf",
            };

}

bundle agent make_conf(root, role, multilib)

{

  vars:

    have_cpu2use_app&(!broken_cpu2use)::

      "cpu_flags" string => execresult("/$(cryptoo:file_names.cpu2use_app)","noshell");

    (!have_cpu2use_app)|(broken_cpu2use)::

      "cpu_flags" string => "CPU_FLAGS_X86=\"\"";

  classes:

    any::

      "have_cpu2use_app"
        expression => fileexists("/$(cryptoo:file_names.cpu2use_app)");

      "have_layman_conf"
        expression => fileexists("$(root)$(cryptoo:file_names.overlay_conf)");

      "make_conf_role_$(role)"
        expression => "any";

      "make_conf_$(multilib)"
        expression => "any";

    have_cpu2use_app::

      "broken_cpu2use"
        expression => regextract(".*Traceback.*", "$(cpu_flags)", "broken_cpu2use");

    make_conf_role_firewall_external::

      "make_conf_role_guest"
        expression => "any";

    make_conf_role_web::

      "make_conf_role_guest"
        expression => "any";

    make_conf_role_mail_client::

      "make_conf_role_guest"
        expression => "any";

    make_conf_role_web|make_conf_role_mail_client|make_conf_role_template_desktop::

      "make_conf_group_desktop_guest"
        expression => "any";

    make_conf_role_template_desktop|make_conf_role_template_server|make_conf_group_desktop_guest::

      "make_conf_group_guest"
        expression => "any";

  files:

    !broken_cpu2use::

      "$(root)$(cryptoo:file_names.pm_config)"
        perms => system,
        create => "true",
        edit_template => "/$(cryptoo:templates.template[make_conf])";

    broken_cpu2use::

      "$(root)$(cryptoo:file_names.pm_config)"
        delete => default:tidy;

}