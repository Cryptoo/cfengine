body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/common/systemd/networkd.cf",
              "cryptoo/common/systemd/service.cf",
              "cryptoo/config/file_groups.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/config/templates.cf",
              "cryptoo/util/contain.cf",
              "cryptoo/util/perms.cf"
            };

}

bundle agent systemd(root, role)

{

  vars:

    any::

      "hostname"
        string => '{ "value": "$(machine.hostname)" }';

  classes:

    any::

      "have_systemctl"
        expression => fileexists("$(root)$(file_names.systemd_app)");

      "bootstrap"
        not => strcmp("$(this.root)", "/");

  files:

    any::

      "$(root)$(file_names.hostname_systemd)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[value])",
        template_method => "mustache",
        template_data => parsejson($(this.hostname));

      "$(root)$(file_names.cf_execd_service)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[cf_execd])",
        classes => default:if_repaired("daemon_reload");

      "$(root)$(file_names.cf_monitord_service)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[cf_monitord])",
        classes => default:if_repaired("daemon_reload");

      "$(root)$(file_names.cf_serverd_service)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[cf_serverd])",
        classes => default:if_repaired("daemon_reload");

      "$(root)$(file_names.ipset_service)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[ipset_service])",
        classes => default:if_repaired("daemon_reload");

      "$(root)$(file_names.shorewall_service)"
        perms => system,
        create => "true",
        edit_template => "/$(templates.template[shorewall_service])",
        classes => default:if_repaired("daemon_reload");

  methods:

    any::

      "configure systemd-networkd"
        usebundle => networkd($(this.root), $(this.role));

    have_systemctl&(!bootstrap)::

      "enable systemd services"
        usebundle => systemd_enable($(root), $(file_groups.systemd_enabled[$(this.role)]));

# This bundle always attempts to start systemd-networkd even though it's already running.
#      "start systemd services"
#        usebundle => default:standard_services( $(file_groups.systemd_enabled[$(this.role)]), "start");

  commands:

    daemon_reload&have_systemctl&(!bootstrap)::

      "/$(file_names.systemd_app)"
        args => "daemon-reload",
        contain => basic_chroot($(root));

}