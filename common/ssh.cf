body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/config/config.cf",
              "cryptoo/config/directory_names.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/util/link_from.cf"
            };

}

bundle agent ssh(root, role)

{
  classes:

    any::

      "ssh_role_$(this.role)"
        expression => "any";

      "have_ssh"
        expression => fileexists("$(root)$(cryptoo:file_names.ssh_app)");

      "have_sshd"
        expression => fileexists("$(root)$(cryptoo:file_names.sshd_app)");

      "have_master_ssh_key"
        expression => fileexists("/$(file_names.master_ssh_key)");

  files:

    have_sshd::

      "$(root)$(cryptoo:file_names.sshd_config)"
        create => "false",
        edit_line => default:set_line_based("cryptoo:config.ssh[sshd]", " ", "\s+", ".*", "\s*#\s*"),
        classes => default:if_repaired("update_sshd");

      "$(root)$(cryptoo:directory_names.fs_config_ssh)/$(cryptoo:config.ssh[banned_keys])"
        move_obstructions => "true",
        link_from => cryptoo:null,
        classes => default:if_repaired("update_sshd");

    have_master_ssh_key&(!ssh_role_master)&(!ssh_role_desktop_host)::

      "/$(file_names.authorized_keys)"
        perms => root,
        move_obstructions => "true",
        copy_from => default:local_cp("/$(cryptoo:file_names.master_ssh_key)");

}
