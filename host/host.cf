body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/cryptoo.cf",
              "cryptoo/config/directory_names.cf",
              "cryptoo/config/directory_groups.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/config/machines.cf",
              "cryptoo/config/templates.cf",
              "cryptoo/common/common.cf",
              "cryptoo/host/crypttab.cf",
              "cryptoo/host/guest_boxes.cf",
              "cryptoo/host/kernel.cf",
              "cryptoo/host/mounts.cf",
              "cryptoo/host/openrc.cf",
              "cryptoo/util/perms.cf",
              "cryptoo/host/subnet.cf",
              "cryptoo/util/classes.cf",
              "cryptoo/util/link_from.cf",
              "cryptoo/util/setfattr.cf",
              "cryptoo/host/guest_role.cf",
            };

}

bundle agent host(arch)

{

  vars:

    any::

      "layman_cache_list"
        slist => findfiles("/$(cryptoo:directory_names.fs_volatile_overlay)/cache_*.xml");

      "layman_cache"
        string => nth("layman_cache_list", 0);

      "portage_cleanup_command"
        string => "/$(cryptoo:file_names.find_app) /$(cryptoo:directory_names.fs_mount_root)/$(cryptoo:directory_names.fs_pm) -mindepth 1 -maxdepth 1 -name * -exec /$(cryptoo:file_names.move_app) {} /$(cryptoo:directory_names.fs_storage_pm) ;",
        comment => "this command moves /mnt/root/usr/portage/* /usr/portage/. It's needed for bootstrapping, because /usr/portage is populated before it's mounted in the proper location";

      "distfiles_cleanup_command"
        string => "/$(cryptoo:file_names.find_app) /$(cryptoo:directory_names.fs_storage_pm)/distfiles -mindepth 1 -maxdepth 1 -name * -exec /$(cryptoo:file_names.move_app) {} /$(cryptoo:directory_names.fs_storage_srcpkg) ;",
        comment => "this command moves the contents of /usr/portage/distfiles to /usr/site-local/distfiles. Also needed for bootstrapping";

  classes:

    any::

      "$(arch)"
        expression => "any";

      "host"
        expression => "any";

      "root_bind_mount"
        expression => regline("device /dev/mapper/$(cryptoo:machines.hostname)-root mounted on /$(cryptoo:directory_names.fs_mount_root).*", "$(cryptoo:file_names.mount_info)"),
        comment => "/ is mounted as a bind mount on /mnt/root. Gives access to underlying rootfs";

      "srv_mounted"
        expression => regline("device /dev/mapper/$(cryptoo:machines.hostname)-storage mounted on /$(cryptoo:directory_names.fs_storage_base).*", "$(cryptoo:file_names.mount_info)"),
        comment => "backing LV is mounted for /srv";

      "have_grub"
        expression => fileexists("/$(cryptoo:file_names.grub_defaults)");

      "have_cryptoo"
        expression => fileexists("/$(cryptoo:file_names.cryptoo_profile)");

      "need_cryptoo"
        not => fileexists("/$(cryptoo:file_names.cryptoo_profile)");

      "have_crypttab"
        expression => fileexists("/$(cryptoo:file_names.crypttab)");

      "need_crypttab"
        not => fileexists("/$(cryptoo:file_names.crypttab)");

      "have_dracut_app"
        expression => fileexists("/$(cryptoo:file_names.dracut_app)");

      "have_dracut_image"
        expression => fileexists("/$(cryptoo:file_names.dracut_image)");

      "need_dracut_image"
        not => fileexists("/$(cryptoo:file_names.dracut_image)");

      "have_layman_app"
        expression => fileexists("/$(cryptoo:file_names.layman_app)");

      "have_layman_cache"
         expression => fileexists("$(layman_cache)");

      "need_layman_cache"
         not => fileexists("$(layman_cache)");

      "need_bitcoin_overlay"
        not => fileexists("/$(cryptoo:file_names.bitcoin_overlay)");

      "have_libvirtd_app"
        expression => fileexists("/$(cryptoo:file_names.libvirtd_app)");

    srv_started&(!srv_failed)::

      "srv_ready"
        expression => "any",
        comment => "every directory under /srv was created, and none failed";

    srv_ready&(!srv_created)::

      "all_ready"
        expression => "any",
        comment => "this is the normal case, because we normally don't need to create /srv";

  files:

    any::

      "/$(cryptoo:directory_groups.host_system)/."
        perms => cryptoo:system,
        create => "true",
        handle => "done_$(cryptoo:directory_groups.host_system)",
        comment => "create host-specific directories";

      "/$(cryptoo:directory_groups.host_system_private)/."
        perms => cryptoo:root,
        create => "true",
        handle => "done_$(cryptoo:directory_groups.host_system)",
        comment => "create host-specific root-only directories";

      "/$(cryptoo:file_names.dmcrypt_key)"
        perms => cryptoo:root,
        create => "false",
        comment => "ensure master HD encryption key is root-only";

      "/$(cryptoo:directory_groups.host_root_qemu)/."
        perms => cryptoo:root_qemu,
        create => "true",
        comment => "/etc/cryptoo/guest/{inbox,outbox}";

      "/$(cryptoo:file_names.dracut_conf)"
        perms => cryptoo:system,
        create => "true",
        edit_template => "/$(cryptoo:templates.template[dracut_conf])",
        classes => default:u_if_repaired("update_dracut"),
        comment => "configure dracut";

      "/$(cryptoo:file_names.env_kernel)"
        perms => cryptoo:system,
        create => "true",
        edit_template => "/$(cryptoo:templates.template[env_kernel])",
        classes => default:u_if_repaired("update_env"),
        comment => "create /etc/env.d/99kernel";

      "/$(cryptoo:file_names.fstab)"
        perms => cryptoo:system,
        create => "true",
        edit_template => "/$(cryptoo:templates.template[fstab])",
        classes => default:u_if_repaired("fstab_repaired"),
        comment => "configure /etc/fstab";

      "/$(cryptoo:directory_names.fs_storage_base)/."
        perms => cryptoo:system,
        create => "true",
        classes => srv,
        comment => "create /srv mountpoint";

    have_grub::

      "/$(cryptoo:file_names.grub_defaults)"
        perms => cryptoo:system,
        create => "false",
        edit_line => default:delete_lines_matching("# GRUB_CMDLINE_LINUX=.*"),
        comment => "remove some example comments that interfere with the next promise";

      "/$(cryptoo:file_names.grub_defaults)"
        perms => cryptoo:system,
        create => "false",
        edit_line => default:set_quoted_values("cryptoo:config.grub"),
        comment => "configure /etc/default/grub";

    root_bind_mount::

      "/$(cryptoo:directory_names.fs_mount_root)/$(cryptoo:file_names.dmcrypt_key)"
        perms => cryptoo:root,
        copy_from => default:local_cp("/$(cryptoo:file_names.dmcrypt_key)"),
        comment => "put a backup copy of the master HD encryption inside the encrypted volume";

    srv_mounted::

      "/$(cryptoo:directory_groups.host_system_storage)/."
        perms => cryptoo:system,
        create => "true",
        classes => srv_ready,
        comment => "create system directories under /srv";

      "/$(cryptoo:directory_groups.host_pm)/."
        perms => cryptoo:portage,
        create => "true",
        classes => srv_ready,
        comment => "create Portage-owned directories under /srv";

      "/$(cryptoo:directory_groups.host_pm_open)/."
        perms => cryptoo:portage_open,
        create => "true",
        classes => srv_ready,
        handle => "done_$(cryptoo:directory_groups.host_pm_open)",
        comment => "create Portage-owned writables directories under /srv";

      "/$(cryptoo:directory_names.fs_storage_shared)/."
        depends_on => { "users_ok" },
        perms => cryptoo:guest,
        create => "true",
        classes => srv_ready,
        comment => "create master common storage directory";

      "/$(cryptoo:file_names.storage_pm_site_meta)"
        perms => cryptoo:system,
        create => "true",
        edit_template => "/$(cryptoo:templates.template[local_layout_conf])";

      "/$(cryptoo:file_names.storage_pm_site_repo)"
        perms => cryptoo:system,
        create => "true",
        edit_template => "/$(cryptoo:templates.template[site_repo_name])";

    need_machine_names::

      "/$(cryptoo:file_names.machine_names)"
        perms => cryptoo:system,
        create => "true",
        edit_template => "/$(cryptoo:templates.template[machine_names])";

    need_site_config::

      "/$(cryptoo:file_names.site_config)"
        perms => cryptoo:system,
        create => "true",
        edit_template => "/$(cryptoo:templates.template[site_config])",
        classes => default:u_if_repaired("site_config_available");

    have_libvirtd_app::

      "/$(cryptoo:directory_groups.host_qemu)/."
        perms => cryptoo:guest,
        create => "true",
        handle => "done_$(cryptoo:directory_groups.host_qemu)",
        comment => "create qemu storage directories";

    have_libvirtd_app&srv_mounted::

      "/$(cryptoo:directory_names.fs_storage_qemu)/."
        perms => cryptoo:guest,
        create => "true",
        handle => "done_$(cryptoo:directory_names.fs_storage_qemu)",
        comment => "create base qemu storage directory";

  methods:

    any::

      "configure OpenRC"
        usebundle => cryptoo:openrc("/");

      "manage files for the server skeleton"
        usebundle => cryptoo:cryptoo_filesystem("/$(cryptoo:directory_names.fs_volatile_images)/server/x86_64/no-multilib/","template_server","x86_64","nomultilib");

      "manage files for the desktop skeleton"
        usebundle => cryptoo:cryptoo_filesystem("/$(cryptoo:directory_names.fs_volatile_images)/desktop/x86_64/no-multilib/","template_desktop","x86_64","nomultilib");

      "manage files for the desktop-multilib skeleton"
        usebundle => cryptoo:cryptoo_filesystem("/$(cryptoo:directory_names.fs_volatile_images)/desktop/x86_64/multilib/","template_desktop","x86_64","multilib");

      "manage crypttab"
        usebundle => cryptoo:crypttab;

      "manage guest inboxes and outboxes"
        usebundle => cryptoo:guest_boxes;

    (all_mounted)|(all_ready)::

      "manage common files"
        usebundle => cryptoo:cryptoo_filesystem("/","master","$(arch)","nomultilib");

      "configure host kernel"
        usebundle => cryptoo:kconfig("host", "$(arch)"),
        classes => default:if_ok("kernel_ok");

      "configure guest kernel"
        usebundle => cryptoo:kconfig("guest", "$(arch)"),
        classes => default:if_ok("kernel_ok");

      "configure multilib guest kernel"
        usebundle => cryptoo:kconfig("guest_multilib", "$(arch)"),
        classes => default:if_ok("kernel_ok");

    srv_created::

      "mount storage device"
        usebundle => cryptoo:mount_types("ext4"),
        classes => default:if_ok("srv_mounted");

    srv_ready&srv_created::

      "ensure all mounts are active"
        usebundle => cryptoo:mount_all,
        classes => default:if_ok("all_mounted");

    fstab_repaired::

      "activate all mountpoints"
        usebundle => cryptoo:mount_all,
        depends_on=> { "srv_ok" };

    need_subnet_config::

      "generate random subnets"
        usebundle => cryptoo:subnet,
        classes => default:if_ok("subnet_config_available");

  commands:

    update_env::

      "/$(cryptoo:file_names.envupdate_app)";

    need_cryptoo&(all_mounted|all_ready)::

      "/$(file_names.git_app) clone -b dev $(config.git_repositories[overlay]) /$(directory_names.fs_storage_cryptoo)";

    (need_dracut_image|update_dracut)&(have_dracut_app&have_crypttab)::

      "/$(cryptoo:file_names.dracut_app) --force --no-compress /$(cryptoo:file_names.dracut_image)";

    need_layman_cache&have_layman_app::

    "/$(cryptoo:file_names.layman_app) -f";

    need_bitcoin_overlay&have_layman_cache::

      "/$(cryptoo:file_names.layman_app) -a bitcoin";

    all_mounted::

      "$(portage_cleanup_command)";

      "$(distfiles_cleanup_command)";

    (all_mounted|all_ready)&have_cryptoo::

      "/$(cryptoo:file_names.distfiles_perms_app)";

      "/$(cryptoo:file_names.pkgdir_perms_app)";
}
