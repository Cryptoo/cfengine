body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/config/config.cf",
              "cryptoo/config/directory_groups.cf",
              "cryptoo/config/file_names.cf",
              "cryptoo/util/contain.cf",
              "cryptoo/util/perms.cf"
            };

}

bundle agent common_guest(root, role, arch)

{

  classes:

    any::

      "have_systemctl"
        expression => fileexists("$(root)$(file_names.systemd_app)");

  files:

    any::

      "$(root)$(directory_groups.guest_system)/."
        perms => cryptoo:system,
        create => "true",
        handle => "$(root)$(directory_groups.guest_system)";

      "$(root)$(directory_groups.guest_system_private)/."
        perms => cryptoo:root,
        create => "true",
        handle => "$(root)$(directory_groups.guest_system_private)";

      "$(root)$(file_names.cf_execd_service)"
        perms => cryptoo:system,
        create => "true",
        edit_template => "/$(templates.template[cf_execd])",
        classes => default:u_if_repaired("daemon_reload");

      "$(root)$(file_names.cf_monitord_service)"
        perms => cryptoo:system,
        create => "true",
        edit_template => "/$(templates.template[cf_monitord])",
        classes => default:u_if_repaired("daemon_reload");

      "$(root)$(file_names.cf_serverd_service)"
        perms => cryptoo:system,
        create => "true",
        edit_template => "/$(templates.template[cf_serverd])",
        classes => default:u_if_repaired("daemon_reload");

  commands:

    have_systemctl::

      "/$(file_names.systemd_app)"
        args => "enable $(config.server_guest_services)",
        contain => silent_chroot($(root)),
        classes => always_success;

    daemon_reload&have_systemctl::

      "/$(file_names.systemd_app)"
        args => "daemon-reload",
        contain => basic_chroot($(root));

}
