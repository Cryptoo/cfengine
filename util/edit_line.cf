body file control

{

  namespace => "cryptoo";

  inputs => {
              "cryptoo/util/location.cf",
              "cryptoo/util/replace_with.cf"
            };

}

bundle edit_line bashrc

{
    replace_patterns:

      "(.*PS1=.*)W(.*)"
        replace_with => cryptoo:PS1_small_w;

    insert_lines:

      "alias ls='ls -FADv -I .keep --color=auto'$(const.n)alias nano='nano -w'"
        location => cryptoo:after_unset;
}

bundle edit_line enable_kconfig(symbol, file)

{
     classes:

       "exists_disabled"
         expression => regline("# CONFIG_$(symbol) is not set", "$(file)");

       "missing_from_file"
         not => "exists_disabled";
 
     replace_patterns:

      exists_disabled::

       "# CONFIG_$(symbol) is not set"
         replace_with => cryptoo:enable_kernel_symbol("$(symbol)");

     insert_lines:

      missing_from_file::

       "CONFIG_$(symbol)=y"
         location => cryptoo:first_line;

     reports:

       "($(this.file)) enabled CONFIG_$(symbol)";

}

bundle edit_line disable_kconfig(symbol,file)

{
     classes:

       "exists_enabled"
         expression => regline("CONFIG_$(symbol)=.*", "$(file)");

       "missing_from_file"
         not => "exists_enabled";
 
     replace_patterns:

      exists_enabled::

       "CONFIG_$(symbol)=y"
         replace_with => cryptoo:disable_kernel_symbol("$(symbol)");

     insert_lines:

      missing_from_file::

       "# CONFIG_$(symbol) is not set"
         location => cryptoo:first_line;

     reports:

      exists_enabled::

        "toggled CONFIG_$(symbol)";

      missing_from_file::

        "($(this.file)) added CONFIG_$(symbol)";

}