bundle edit_line bashrc

{
    replace_patterns:

      "(.*PS1=.*)W(.*)"
        replace_with => PS1_small_w;

    insert_lines:

      "alias ls='ls -FADv -I .keep --color=auto'$(const.n)alias nano='nano -w'"
        location => after_unset;
}

bundle edit_line enable_kconfig(symbol, file)

{
     classes:

       "exists_disabled" expression => regline("# CONFIG_$(symbol) is not set", "$(file)");
       "missing_from_file" not => "exists_disabled";
 
     replace_patterns:

      exists_disabled::

       "# CONFIG_$(symbol) is not set"
         replace_with => enable_kernel_symbol("$(symbol)");

     insert_lines:

      missing_from_file::

       "CONFIG_$(symbol)=y"
         location => first_line;

     reports:

       "enabled CONFIG_$(symbol)";

}

bundle edit_line disable_kconfig(symbol,file)

{
     classes:

       "exists_enabled" expression => regline("CONFIG_$(symbol)=.*", "$(file)");
       "missing_from_file" not => "exists_enabled";
 
     replace_patterns:

      exists_enabled::

       "CONFIG_$(symbol)=y"
         replace_with => disable_kernel_symbol("$(symbol)");

     insert_lines:

      missing_from_file::

       "# CONFIG_$(symbol) is not set"
         location => first_line;

     reports:

      exists_enabled::

        "toggled CONFIG_$(symbol)";

      missing_from_file::

        "added CONFIG_$(symbol)";

}

