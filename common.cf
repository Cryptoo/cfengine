bundle common cryptoo

{

  vars:

    any::

      "bundles" slist => {
                            "cryptoo",
                            "config",
                            "directory_names",
                            "directory_groups",
                            "file_names",
                            "cryptoo_system"
                         };

      "inputs" slist => {
                            "cryptoo/config/config.cf",
                            "cryptoo/config/directory_names.cf",
                            "cryptoo/config/directory_groups.cf",
                            "cryptoo/config/file_names.cf",
                            "cryptoo/common/common.cf",
                            "cryptoo/common/gcc.cf",
                            "cryptoo/guest/template/files.cf",
                            "cryptoo/host/crypttab.cf",
                            "cryptoo/host/host.cf",
                            "cryptoo/host/kernel.cf",
                            "cryptoo/host/openrc.cf",
                            "cryptoo/host/subnet.cf",
                            "cryptoo/util/classes.cf",
                            "cryptoo/util/contain.cf",
                            "cryptoo/util/edit_line.cf",
                            "cryptoo/util/link_from.cf",
                            "cryptoo/util/location.cf",
                            "cryptoo/util/perms.cf",
                            "cryptoo/util/replace_with.cf",
                            "cryptoo/mounts.cf",
                            "cryptoo/system.cf"
                         };


      "kernel_git"
        string => "git://github.com/cryptoo/kernel";

      "overlay_git"
        string => "git://github.com/cryptoo/overlay";

    have_machine_names::

      "machine_names" data => readjson("/$(file_names.machine_names)", 1M);

    need_machine_names::

      "machine_names" data => readjson("/$(file_names.template_machine_names)", 1M);

    have_site_config::

      "site_config" data => readjson("/$(file_names.site_config)", 1M),
        classes => if_ok("site_config_available");

    need_site_config::

      "site_config" data => readjson("/$(file_names.template_site_config)", 1M),
        classes => if_ok("site_config_available");

    have_subnet_config::

      "subnet_config" data => readjson("/$(file_names.subnet_config)", 1M),
        classes => if_ok("subnet_config_available");

    role_master::

      "machine_number" string => "1";

  classes:

    any::

      "role_master"
        expression => regline("master", "/$(file_names.crypoo_role)");

      "role_undefined"
        not => fileexists("/$(file_names.crypoo_role)");

      "have_machine_names"
        expression => fileexists("/$(file_names.machine_names)");

      "need_machine_names"
        not => fileexists("/$(file_names.machine_names)");

      "have_site_config"
        expression => fileexists("/$(file_names.site_config)");

      "need_site_config"
        not => fileexists("/$(file_names.site_config)");

      "have_subnet_config"
        expression => fileexists("/$(file_names.subnet_config)");

      "need_subnet_config"
        not => fileexists("/$(file_names.subnet_config)");

  reports:

    role_master::

      "My role is: master";
      "My number is: $(machine_number)";
      "My hostname is: $(machine_names[$(machine_number)])";

    role_undefined::

      "My role is: undefined";

}
